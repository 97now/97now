<img width="672" height="297" alt="Image" src="https://github.com/user-attachments/assets/93d41cc6-2bf4-46d3-901f-beda41bac8b9" />

<img width="687" height="297" alt="Image" src="https://github.com/user-attachments/assets/9e7340f0-02d2-4c02-871b-fe26f137febc" />

<img width="652" height="190" alt="Image" src="https://github.com/user-attachments/assets/1954f5ea-7fe7-41c8-b844-87b979c455dc" />

```
" 해당 부분이 있다면 위 사진처럼 보인다.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_carrid. " => 리스트 박스는 도움말 버튼이라는게
" 없고 검색화면이 나오자마자 출력되야해서 검색화면 나오기 전에 실행.
" 기존에 도움말이 있다면 수정해주는 역할도 한다.
" 내장된거 쓰지 않고 내가 만들어서 넣을 수 있다.
REFRESH LT_DROPLIST.

SELECT
  CARRID AS KEY,
  CARRID && ' ' && CARRNAME AS TEXT
  FROM SCARR
  INTO TABLE @LT_DROPLIST.

SORT LT_DROPLIST BY KEY TEXT.

DELETE ADJACENT DUPLICATES FROM LT_DROPLIST.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id                    = 'p_carrid'
      values                = LT_DROPLIST.
*   EXCEPTIONS
*     ID_ILLEGAL_NAME       = 1
*     OTHERS                = 2
*            .
  IF sy-subrc = 0.
* Implement suitable error handling here
  ENDIF.

*inter = 10.
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_period.

REFRESH LT_DROPLIST.

*SELECT
*  PERIOD AS KEY,
*  PERIOD AS TEXT
*  FROM SPFLI
*  INTO TABLE @LT_DROPLIST.

SELECT
  CAST( PERIOD AS CHAR ) AS KEY,
  CAST( PERIOD AS CHAR ) AS TEXT
  FROM SPFLI
  INTO TABLE @LT_DROPLIST.

SORT LT_DROPLIST BY KEY TEXT.

DELETE ADJACENT DUPLICATES FROM LT_DROPLIST.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id                    = 'p_period'
      values                = LT_DROPLIST.
*   EXCEPTIONS
*     ID_ILLEGAL_NAME       = 1
*     OTHERS                = 2
*            .
  IF sy-subrc = 0.
* Implement suitable error handling here
  ENDIF.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_connid.

REFRESH LT_DROPLIST.

SELECT
  CONNID AS KEY,
  CARRID && ' ' && CITYFROM && '=>' && CITYTO AS TEXT
*  CARRID && CAST( CONNID AS CHAR ) AS TEXT
*  CARRID && CONNID AS TEXT
  FROM SPFLI
  INTO TABLE @LT_DROPLIST.

SORT LT_DROPLIST BY KEY TEXT.

DELETE ADJACENT DUPLICATES FROM LT_DROPLIST.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id                    = 'p_connid'
      values                = LT_DROPLIST.
*   EXCEPTIONS
*     ID_ILLEGAL_NAME       = 1
*     OTHERS                = 2
*            .
  IF sy-subrc = 0.
* Implement suitable error handling here
  ENDIF.
```

없다면 아래 사진처럼 나옴

<img width="660" height="143" alt="Image" src="https://github.com/user-attachments/assets/48d6271c-d80a-49e2-8272-c264f5257ad6" />

```
" VRM 구조
*-- Single Value in Value Set
       BEGIN OF VRM_VALUE,
         KEY(40) TYPE C, " 40자리를 넘을 수 없다. 문자가 아닌거는 못넣는다.
         TEXT(80) TYPE C, " 80자리를 넘을 수 없다. 문자가 아닌거는 못넣는다.
       END OF VRM_VALUE,
*-- Table of Values
       VRM_VALUES TYPE VRM_VALUE OCCURS 0,
```

<img width="994" height="629" alt="Image" src="https://github.com/user-attachments/assets/eb8c1f36-815c-4c80-8b7c-df31ac0e3d43" />

```
SELECT
  CARRID AS KEY, " 필드 이름이 달라도 넣을 수 있고
  CARRID && ' ' && CARRNAME AS TEXT "(&& : 문자열을 합치는 부분) 두개를 동시에 넣을 수 있음.
  FROM SCARR
  INTO TABLE @LT_DROPLIST.
  " 값이 위 사진처럼 들어간다.
```

```
*SELECT
*  PERIOD AS KEY,
*  PERIOD AS TEXT
*  FROM SPFLI
*  INTO TABLE @LT_DROPLIST.

SELECT
  CAST( PERIOD AS CHAR ) AS KEY,
  CAST( PERIOD AS CHAR ) AS TEXT
  FROM SPFLI
  INTO TABLE @LT_DROPLIST.
" VRM 구조에 의해서 형식을 맞춰줘야 해서 아래와 같이 입력
" CAST를 통해 형변환을했음.
```

```
SELECT
  CONNID AS KEY,
  CARRID && ' ' && CITYFROM && '=>' && CITYTO AS TEXT
*  CARRID && CAST( CONNID AS CHAR ) AS TEXT : &&쓸때는 CAST도 못쓴다.
*  CARRID && CONNID AS TEXT : 이렇게 하려고 했는데 &&는 문자만 가능하다 connid는 numc타입이라 안됐음
  " 실제로는 KEY값으로 들어가고 보여지는건 TEXT로 보여진다.
  FROM SPFLI
  INTO TABLE @LT_DROPLIST.
```

### OUTER JOIN
```
A
aa  bb

11  aa
22  bb
33  cc
==============
B
bb  cc

aa  10
cc  20
dd  30

SELECT * FROM A LEFT OUTER JOIN B ON A.aa = B.aa
aa  bb  cc

11  aa  10
22  bb
33  cc  20

SELECT * FROM A RIGHT OUTER JOIN B ON A.aa = B.aa
bb  cc  aa

aa  10  11
cc  20  33
dd  30

"ABAP에는 없는 문법
SELECT * FROM A FULL OUTER JOIN B ON a.bb = B.bb
aa  bb  cc

11  aa  10
22  bb  
33  cc  20
    dd  30

"한행으로 연결가능한 것만 보여주는거
SELECT * FROM A INNER JOIN B ON A.bb = B.bb 
aa  bb  cc

11  aa  10
33  cc  20

"CROSS JOIN 전체 행의 결과
SELECT * FROM A CROSS JOIN B
" 양쪽 테이블의 모든 결합 조건을 다 보여준다. ON 조건이 필요 없음.
" 전체 행 개수 = 행 * 행
```

se38 < 프로그램 바로 조회하는것

DEMO_JOINS 입력
```
SELECT FROM demo_join2 AS t2
         INNER JOIN demo_join1 AS t1 ON t1~d = t2~d
       FIELDS t1~a AS a1,
              t1~b AS b1,
              t1~c AS c1,
              t1~d AS d1,
              t2~d AS d2,
              t2~e AS e2,
              t2~f AS f2,
              t2~g AS g2,
              t2~h AS h2
       ORDER BY t1~d
       INTO CORRESPONDING FIELDS OF TABLE @itab.
       " 필드이름 안넣어도 알아서 필드 이름이 같은 것끼리 들어간다. 구조가 달라도 가능.
       " * 쓰고 필드이름 없이 해도 알아서 들어간다.
       " 선택하는 필드 이름 순서가 itab과 같으면 INTO TABLE만 써도 된다.
```
### 과제

sbook

- 첫번째 박스

타이틀 검색조건

파라미터 3개 ⇒ 필수값 /  default 조건 넣기.

carrid Airline 

connid Connection Number

fldate Flight Date

- 두번째 박스

타이블 고객정보

셀렉트 옵션

customid Customer Number

라디오버튼 3개

r1 All Booking

r2 Valid Booking

r3 Cancelled Booking

- 출력하는 필드

CARRID KEY

CONNID KEY LZERO

FLDATE KEY

BOOKID KEY LZERO

CUSTOMID LZERO HOTSPOT

LOCCURAM

LOCCURKEY

ORDER_DATE

CANCELLED

- 첫번째 PERFORM 구문에 

IF r1 = ‘X’.

CANCELLED 조건 없이(아예 안 넣으면됨.)

ELSEIF r2 = ‘X’.

AND CANCELLED <> ‘X’

ELSEIF r3 = ‘X’.

AND CANCELLED = ‘X’.

ENDIF.

---
<img width="957" height="241" alt="Image" src="https://github.com/user-attachments/assets/0851d58f-a5aa-4231-8293-4c462c49a59c" />

<img width="956" height="243" alt="Image" src="https://github.com/user-attachments/assets/4997fc44-5c83-48d1-bc59-fde0b9c5735a" />

<img width="960" height="306" alt="Image" src="https://github.com/user-attachments/assets/42b5f0e9-227e-4814-a4ee-b936f8d6f8ef" />

<img width="960" height="609" alt="Image" src="https://github.com/user-attachments/assets/7713d3ea-d951-4923-84fe-a3d669a83126" />

<img width="963" height="881" alt="Image" src="https://github.com/user-attachments/assets/40631af6-c6d3-4239-a164-090ff68b4aad" />

<img width="681" height="535" alt="Image" src="https://github.com/user-attachments/assets/1ef3bd2c-ee19-4480-8bab-6ee107d1dd2b" />

<img width="684" height="541" alt="Image" src="https://github.com/user-attachments/assets/31f018e5-e589-4103-9ce6-12165ab2b06c" />

<img width="683" height="294" alt="Image" src="https://github.com/user-attachments/assets/2e86aa9f-1a26-4584-9ce8-d5f353223db6" />

```
*&---------------------------------------------------------------------*
*& Report Z7WEEK_HOMEWORK
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT Z7WEEK_HOMEWORK.

TABLES: sbook.
TYPE-POOLS: slis.

TYPES: BEGIN OF t_sbook,
  carrid     TYPE sbook-carrid,
  connid     TYPE sbook-connid,
  fldate     TYPE sbook-fldate,
  bookid     TYPE sbook-bookid,
  customid   TYPE sbook-customid,
  loccuram   TYPE sbook-loccuram,
  loccurkey  TYPE sbook-loccurkey,
  order_date TYPE sbook-order_date,
  cancelled  TYPE sbook-cancelled,
END OF t_sbook.

" ALV에 표시될 최종 데이터 테이블
DATA: it_sbook TYPE STANDARD TABLE OF t_sbook,
      wa_sbook TYPE t_sbook.

DATA: fieldcatalog TYPE slis_t_fieldcat_alv WITH HEADER LINE, " ALV 필드 속성 정의
      gd_layout    TYPE slis_layout_alv,                      " ALV 레이아웃 설정
      gd_repid     LIKE sy-repid.                             " 현재 프로그램 이름 저장

DATA: lt_droplist TYPE vrm_values,          " 리스트 박스용 내부 테이블
      ls_value    LIKE LINE OF lt_droplist. " 리스트 박스 항목 구조

" 화면 구성
" 검색 조건
SELECTION-SCREEN BEGIN OF BLOCK part1 WITH FRAME TITLE text-001.
  PARAMETERS:
    p_carrid TYPE sbook-carrid AS LISTBOX VISIBLE LENGTH 20 OBLIGATORY DEFAULT 'AA',    "리스트 박스에 필수 입력 기본값 세팅
    p_connid TYPE sbook-connid AS LISTBOX VISIBLE LENGTH 20 OBLIGATORY DEFAULT '0017',  "리스트 박스에 필수 입력 기본값 세팅
    p_fldate TYPE sbook-fldate OBLIGATORY DEFAULT sy-datum.
SELECTION-SCREEN END OF BLOCK part1.

" 고객 정보
SELECTION-SCREEN BEGIN OF BLOCK part2 WITH FRAME TITLE text-002.
  SELECT-OPTIONS: s_custid FOR sbook-customid.
  PARAMETERS:
    r1 RADIOBUTTON GROUP rad DEFAULT 'X' USER-COMMAND rad,
    r2 RADIOBUTTON GROUP rad,
    r3 RADIOBUTTON GROUP rad.
SELECTION-SCREEN END OF BLOCK part2.


AT SELECTION-SCREEN OUTPUT.

  REFRESH lt_droplist.
  SELECT DISTINCT carrid FROM sbook INTO TABLE @DATA(lt_carrid).
  LOOP AT lt_carrid INTO DATA(lv_carrid).
    ls_value-key  = lv_carrid.
    ls_value-text = lv_carrid.
    APPEND ls_value TO lt_droplist.
  ENDLOOP.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'P_CARRID'
      values = lt_droplist.

  REFRESH lt_droplist.
  SELECT DISTINCT connid FROM sbook INTO TABLE @DATA(lt_connid).
  LOOP AT lt_connid INTO DATA(lv_connid).
    ls_value-key  = lv_connid.
    ls_value-text = lv_connid.
    APPEND ls_value TO lt_droplist.
  ENDLOOP.

  CALL FUNCTION 'VRM_SET_VALUES'
    EXPORTING
      id     = 'P_CONNID'
      values = lt_droplist.

START-OF-SELECTION.
  PERFORM get_data.
  PERFORM build_layout.
  PERFORM display_data.


FORM get_data.
  IF r1 = 'X'. " 전체 예약
    SELECT carrid, connid, fldate, bookid, customid, loccuram, loccurkey, order_date, cancelled
      FROM sbook
      INTO TABLE @it_sbook
      WHERE carrid  = @p_carrid
        AND connid  = @p_connid
        AND fldate  = @p_fldate
        AND customid IN @s_custid.

  ELSEIF r2 = 'X'. " 유효 예약
    SELECT carrid, connid, fldate, bookid, customid, loccuram, loccurkey, order_date, cancelled
      FROM sbook
      INTO TABLE @it_sbook
      WHERE carrid  = @p_carrid
        AND connid  = @p_connid
        AND fldate  = @p_fldate
        AND customid IN @s_custid
        AND cancelled <> 'X'.

  ELSEIF r3 = 'X'. " 취소 예약
    SELECT carrid, connid, fldate, bookid, customid, loccuram, loccurkey, order_date, cancelled
      FROM sbook
      INTO TABLE @it_sbook
      WHERE carrid  = @p_carrid
        AND connid  = @p_connid
        AND fldate  = @p_fldate
        AND customid IN @s_custid
        AND cancelled = 'X'.
  ENDIF.
ENDFORM.

" 필드 카탈로그 정의
FORM display_data.
  PERFORM build_fieldcatalog.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      it_fieldcat        = fieldcatalog[]
      is_layout          = gd_layout
      i_callback_program = sy-repid
    TABLES
      t_outtab           = it_sbook.
ENDFORM.

" 출력할 컬럼 속성과 표시 이름 지정
FORM build_fieldcatalog.

  fieldcatalog-fieldname = 'CARRID'.
  fieldcatalog-seltext_m = 'Airline'.
  fieldcatalog-key = 'X'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'CONNID'.
  fieldcatalog-seltext_m = 'Connection Number'.
  fieldcatalog-key = 'X'.
  fieldcatalog-lzero = 'X'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'FLDATE'.
  fieldcatalog-seltext_m = 'Flight Date'.
  fieldcatalog-key = 'X'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'BOOKID'.
  fieldcatalog-seltext_m = 'Booking ID'.
  fieldcatalog-key = 'X'.
  fieldcatalog-lzero = 'X'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'CUSTOMID'.
  fieldcatalog-seltext_m = 'Customer Number'.
  fieldcatalog-lzero = 'X'.
  fieldcatalog-hotspot = 'X'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'LOCCURAM'.
  fieldcatalog-seltext_m = 'Local Amount'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'LOCCURKEY'.
  fieldcatalog-seltext_m = 'Currency Key'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'ORDER_DATE'.
  fieldcatalog-seltext_m = 'Order Date'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

  fieldcatalog-fieldname = 'CANCELLED'.
  fieldcatalog-seltext_m = 'Cancelled'.
  APPEND fieldcatalog. CLEAR fieldcatalog.

ENDFORM.

FORM build_layout.
  gd_layout-no_input          = 'X'.
  gd_layout-colwidth_optimize = 'X'.
  gd_layout-zebra             = 'X'.
ENDFORM.
```